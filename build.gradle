buildscript {
    repositories {
        mavenCentral()
        maven {
            url = "https://jcenter.bintray.com/"
        }
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath('com.anatawa12.forge:ForgeGradle:1.2-1.1.+') {
            changing = true
        }
    }
}

apply plugin: 'forge'
apply plugin: 'idea'
apply plugin: 'maven-publish'

version = "${version}"
group = "committee.nova.sittable.forge" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Sittable-Forge-1.7.10"
def mcVersion = "1.7.10"

targetCompatibility = sourceCompatibility = '1.8'

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", mcVersion
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand 'version': project.version, 'mcversion': mcVersion
    }
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task devJar(type: Jar) {
    dependsOn jar

    classifier = 'dev'
    // copy the contents of the main jar
    from zipTree(tasks.jar.archivePath)

    manifest = tasks.jar.manifest
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'sittable-1.7.10'
            artifact devJar
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        maven {
            name = "release"
            url = 'https://maven.nova-committee.cn/releases'
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
